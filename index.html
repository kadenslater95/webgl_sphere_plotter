<!DOCTYPE html>
<html>
<head>
  <link rel="stylesheet" href="index.css"></link>

  <!-- Use glmatrix library -->
  <script type="text/javascript" src="gl-matrix-min.js"></script>

  <script type="text/javascript" src="index.js"></script>

  <script type="x-shader/x-vertex" src="vertex.glsl" id="vertex-shader">
    #version 100

    attribute vec2 aPosition;
  
    attribute vec3 aNormal;

    uniform mat4 uModel;
    uniform mat4 uView;
    uniform mat4 uProjection;

    // Inverse-Transpose of upper-left 3x3 matrix of uModel
    uniform mat3 uNormalMatrix;

    varying vec3 vNormal;
    varying vec3 vFragmentPosition;

    void main() {
      float rho = 5.0;

      float x = rho * sin(aPosition.y) * cos(aPosition.x);
      float y = rho * cos(aPosition.y);
      float z = rho * sin(aPosition.y) * sin(aPosition.x);

      vec4 worldPosition = uModel * vec4(x, y, z, 1.0);

      vFragmentPosition = worldPosition.xyz;

      vNormal = normalize(uNormalMatrix * aNormal);

      gl_Position = uProjection * uView * worldPosition;
    }
  </script>

  <script type="x-shader/x-fragment" src="fragment.glsl" id="fragment-shader">
    #version 100

    precision mediump float;

    // Uses Blinn-Phong Lighting

    varying vec3 vNormal;
    varying vec3 vFragmentPosition;

    uniform vec3 uLightPosition; // in world space
    uniform vec3 uCameraPosition; // in world space
    uniform vec3 uLightColor;
    uniform vec3 uObjectColor;

    void main() {
      // Normalize interpolated normal
      vec3 N = normalize(vNormal);

      // Direction from fragment to light
      vec3 L = normalize(uLightPosition - vFragmentPosition);

      // Diffuse
      float diff = max(dot(N, L), 0.0);

      // Ambient
      float ambientStrength = 0.1;
      vec3 ambient = ambientStrength * uLightColor;

      // Specular
      float specularStrength = 0.5;
      vec3 V = normalize(uCameraPosition - vFragmentPosition);
      vec3 H = normalize(L + V); // half vector
      float spec = pow(max(dot(N, H), 0.0), 32.0);

      vec3 diffuse = diff * uLightColor;
      vec3 specular = specularStrength * spec * uLightColor;

      vec3 result = (ambient + diffuse + specular) * uObjectColor;

      gl_FragColor = vec4(result, 1.0);
    }
  </script>
</head>
<body>
  <p id="webgl-status"></p>
  <canvas></canvas>
</body>
</html>